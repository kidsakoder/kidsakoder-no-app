plugins {
    id 'com.enonic.xp.app' version '1.0.17'
    id 'com.moowork.node' version '1.2.0'
}

app {
    name = project.appName
    displayName = project.displayName
    vendorName = project.vendorName
    vendorUrl = project.vendorUrl
    systemVersion = "${xpVersion}"
}

node {
    download = true
    version = '8.9.4'
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.lib:lib-guillotine:1.0.0"
    //include "com.enonic.xp:lib-http-client:${xpVersion}"
    //include "com.enonic.xp:lib-mail:${xpVersion}"
    //include "com.enonic.xp:lib-i18n:${xpVersion}"
    //include "com.enonic.xp:lib-auth:${xpVersion}"
    //include "com.enonic.xp:lib-cache:${xpVersion}"
    include "com.enonic.lib:menu:2.0.1"
    include "com.enonic.lib:util:1.1.2"
    testCompile "junit:junit:4.12"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "http://repo.enonic.com/public"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

/* Task enabling es6 transpiling for Enonic */

task babel(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/@babel/cli/bin/babel.js')
    args = [
        "-d",
        "$buildDir/resources/main",
        "-x",
        ".es6",
        "--presets",
        "@babel/env",
        "src/main/resources"
    ]

    if (project.hasProperty('watch')) {
        args = ['-w'] + args
    }

    inputs.dir 'src/main/resources'
    outputs.dir "$buildDir/resources/main"
}

jar.dependsOn += babel

/* Task for compiling React apps and SASS */

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/webpack/bin/webpack.js')

    if (project.hasProperty('watch')) {
        args = ['-w']
    }
}

jar.dependsOn += webpack

/* Test React apps using ava */

task ava(type: NodeTask) {
    script = file('node_modules/ava/cli.js')
}
